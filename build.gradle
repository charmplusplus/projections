plugins {
  id 'java'
  id 'application'
}

application {
  mainClass = 'projections.analysis.ProjMain'
}

java {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

version = '6.10.0'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.freehep:freehep-graphicsbase:2.4'
  implementation 'org.freehep:freehep-graphics2d:2.4'
  implementation 'org.freehep:freehep-graphicsio:2.4'
  implementation 'org.freehep:freehep-graphicsio-pdf:2.4'
  implementation 'org.freehep:freehep-graphicsio-svg:2.4'
  implementation 'org.freehep:freehep-graphicsio-ps:2.4'
  implementation 'org.freehep:freehep-vectorgraphics:2.4'

  implementation 'org.jfree:jfreechart:1.5.0'
}


sourceSets {
  main {
    java {
      srcDir 'src'
    }
  }
}

tasks.register('fatJar', Jar) {
    archiveBaseName.set('projections-all')
    archiveVersion.set(project.version.toString())
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    manifest {
        attributes(
            'Implementation-Title': 'Projections',
            'Implementation-Version': project.version
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    into('projections/images') {
        from('src/projections/images') {
            include '*'
        }
    }

    into('META-INF/services/') {
        from('src/projections/resources') {
            include 'org.freehep.graphicsbase.util.export.ExportFileType'
        }
    }

    with tasks.jar
}

tasks.register('copyJarToBin', Copy) {
    from(tasks.named('fatJar'))
    into('bin/')
    rename { 'projections.jar' }
}
